package GetSpiderVenom
	import Equipment
	import Game
	import WurstProfile
	import TerrainData
	import SoundsLib
	import Ships
	import SimError

	constant SUCCESS_MESSAGE  = "You acquired a vial of |cffffcc00Spider Venom|r; return to your Utility Port for a reward."
	constant NO_SPACE_MESSAGE = "You don't have inventory space for |cffffcc00Spider Venom|r."

	function spiderAction() returns bool
		profile()

		let fU = GetFilterUnit()
		let slt = Equipment.unitHasItemOfTypeSlot(fU, Equipment.SPIDER_VENOM_CONTRACT)
		let owner = GetOwningPlayer(fU)

		if slt > -1 and (not Ships.hasItemOfType(fU, Equipment.VENOM_VIAL))
			if Equipment.noItemSlot(fU)
				simError(owner, NO_SPACE_MESSAGE)
			else
				UnitAddItem(fU, CreateItem(Equipment.VENOM_VIAL, GetUnitX(fU), GetUnitY(fU)))
				DisplayTextToPlayer(owner, 0., 0., SUCCESS_MESSAGE)
				SoundsLib.playForPlayer(Game.TRADE_SUCCESS_SOUND, 1., owner)

		return false

	init
		let spiderTrigger = CreateTrigger()
		let reg           = CreateRegion()

		RegionAddRect(reg, TerrainData.spider)

		TriggerRegisterEnterRegion(spiderTrigger, reg, Filter(function spiderAction))
