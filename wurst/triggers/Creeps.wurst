package Creeps
	import Game
	import Players
	import TerrainData
	import EnchantingStone

	constant     GRYPH_ID = 'hgry'
	constant    WYVERN_ID = 'owyv'
	constant      HAWK_ID = 'hdhw'
	constant      WYRM_ID = 'ufro'
	constant ENCHANTED_ID = 'u006'

	constant SPAWN_PERIOD = 30.
	constant  HAWK_PERIOD = 30. * 2.
	constant  WYRM_PERIOD = 30. * 3.

	function spawnUpper(int faction, int cls, real x, real face)
		unit u = CreateUnit(Players.players[faction], cls, x, 0., face)
		u.issuePointOrder("attack", TerrainData.POS_STONE1)


	function spawnLower(int faction, int cls, real x, real face)
		unit u = CreateUnit(Players.players[faction], cls, x, 0., face)
		u.issuePointOrder("attack", TerrainData.POS_STONE2)


	function spawn(int faction, int cls)
		real x
		real face

		if faction == 0
			x = TerrainData.WESTX
			face = 0.
		else
			x = TerrainData.EASTX
			face = 180.


		spawnUpper(faction, cls, x, face)

		spawnLower(faction, cls, x, face)


	function base()
		if not Game.ended
			// West units
			spawn(0, GRYPH_ID)
			spawn(0, WYVERN_ID)
			spawn(0, GRYPH_ID)
			spawn(0, WYVERN_ID)

			// East units
			spawn(1, GRYPH_ID)
			spawn(1, WYVERN_ID)
			spawn(1, GRYPH_ID)
			spawn(1, WYVERN_ID)

	function hawk()
		if not Game.ended
			spawn(0, HAWK_ID)
			spawn(1, HAWK_ID)



	function wyrm()
		if not Game.ended
			if EnchantingStone.ownerNorth == Players.players[0]
				spawnUpper(0, ENCHANTED_ID, TerrainData.WESTX,   0.)
				spawnUpper(1,      WYRM_ID, TerrainData.EASTX, 180.)
			else if EnchantingStone.ownerNorth == Players.players[1]
				spawnUpper(0,      WYRM_ID, TerrainData.WESTX,   0.)
				spawnUpper(1, ENCHANTED_ID, TerrainData.EASTX, 180.)
			else
				spawnUpper(0,      WYRM_ID, TerrainData.WESTX,   0.)
				spawnUpper(1,      WYRM_ID, TerrainData.EASTX, 180.)


			if EnchantingStone.ownerSouth == Players.players[0]
				spawnLower(0, ENCHANTED_ID, TerrainData.WESTX,   0.)
				spawnLower(1,      WYRM_ID, TerrainData.EASTX, 180.)
			else if EnchantingStone.ownerSouth == Players.players[1]
				spawnLower(0,      WYRM_ID, TerrainData.WESTX,   0.)
				spawnLower(1, ENCHANTED_ID, TerrainData.EASTX, 180.)
			else
				spawnLower(0,      WYRM_ID, TerrainData.WESTX,   0.)
				spawnLower(1,      WYRM_ID, TerrainData.EASTX, 180.)

	init
		TimerStart(CreateTimer(), SPAWN_PERIOD, true, function base)
		TimerStart(CreateTimer(),  HAWK_PERIOD, true, function hawk)
		TimerStart(CreateTimer(),  WYRM_PERIOD, true, function wyrm)
