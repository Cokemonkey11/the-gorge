package Creeps
	import Ai
	import ClosureTimers
	import Game
	import WurstProfile
	import TerrainData
	import EnchantingStone
	import UnitsApi

	constant ENCHANTED_ID = 'u006'

	constant SPAWN_PERIOD = 30.
	constant  HAWK_PERIOD = 30. * 2.
	constant  WYRM_PERIOD = 30. * 3.

	function spawnUpper(int faction, int cls, real x, real face) returns unit
		let u = CreateUnit(players[faction], cls, x, 0., face)
			..intoCreepAi(Propensity.North)
		return u


	function spawnLower(int faction, int cls, real x, real face) returns unit
		let u = CreateUnit(players[faction], cls, x, 0., face)
			..intoCreepAi(Propensity.South)
		return u


	function spawn(int faction, int cls)
		profile()
		real x
		real face

		if faction == 0
			x = TerrainData.WESTX
			face = 0.
		else
			x = TerrainData.EASTX
			face = 180.


		spawnUpper(faction, cls, x, face)

		spawnLower(faction, cls, x, face)


	function baseSpawn()
		if not Game.ended
			// West units
			spawn(0, ID_GRYPHONRIDER)
			spawn(0, ID_GRYPHONRIDER)
			doAfter(1.) ->
				spawn(0, ID_WINDRIDER)
				spawn(0, ID_WINDRIDER)

			// East units
			spawn(1, ID_GRYPHONRIDER)
			spawn(1, ID_GRYPHONRIDER)
			doAfter(1.) ->
				spawn(1, ID_WINDRIDER)
				spawn(1, ID_WINDRIDER)


	function hawk()
		doAfter(2.) ->
			if not Game.ended
				spawn(0, ID_DRAGONHAWK)
				spawn(1, ID_DRAGONHAWK)


	function wyrm()
		doAfter(3.5) ->
			if not Game.ended
				if EnchantingStone.ownerNorth == players[0]
					spawnUpper(0, ENCHANTED_ID, TerrainData.WESTX,   0.)
						..setVertexColor(155, 155, 255, 225)
					spawnUpper(1, ID_FROSTWYRM, TerrainData.EASTX, 180.)
				else if EnchantingStone.ownerNorth == players[1]
					spawnUpper(0, ID_FROSTWYRM, TerrainData.WESTX,   0.)
					spawnUpper(1, ENCHANTED_ID, TerrainData.EASTX, 180.)
						..setVertexColor(155, 155, 255, 225)
				else
					spawnUpper(0, ID_FROSTWYRM, TerrainData.WESTX,   0.)
					spawnUpper(1, ID_FROSTWYRM, TerrainData.EASTX, 180.)


				if EnchantingStone.ownerSouth == players[0]
					spawnLower(0, ENCHANTED_ID, TerrainData.WESTX,   0.)
						..setVertexColor(155, 155, 255, 225)
					spawnLower(1, ID_FROSTWYRM, TerrainData.EASTX, 180.)
				else if EnchantingStone.ownerSouth == players[1]
					spawnLower(0, ID_FROSTWYRM, TerrainData.WESTX,   0.)
					spawnLower(1, ENCHANTED_ID, TerrainData.EASTX, 180.)
						..setVertexColor(155, 155, 255, 225)
				else
					spawnLower(0, ID_FROSTWYRM, TerrainData.WESTX,   0.)
					spawnLower(1, ID_FROSTWYRM, TerrainData.EASTX, 180.)

	init
		TimerStart(CreateTimer(), SPAWN_PERIOD, true, function baseSpawn)
		TimerStart(CreateTimer(),  HAWK_PERIOD, true, function hawk)
		TimerStart(CreateTimer(),  WYRM_PERIOD, true, function wyrm)
