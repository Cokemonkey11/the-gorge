package SL15ShroudGenerator
	import Abilities
	import AbilitiesApi
	import DummyUnitStack
	import InstantDummyCaster
	import WurstProfile
	import RegisterEvents

	constant GEN_VELOCITY  = 500.
	constant SHROUD_RADIUS = 500.

	class ShroudAbility
		unit caster
		vec2 pos
		real time
		int ticks

		construct(unit caster)
			this.caster = caster
			this.pos = caster.getPos()
			this.time = 0.
			this.ticks = 0

	class ShroudEffect
		static constant EFFECT_RADIUS = 128.

		unit dummy
		effect mdl
		vec2 vel
		real duration

		ondestroy
			this.mdl.destr()
			DummyUnitStack.delayedRelease(dummy, .5)

		construct(vec2 pos, angle ang)
			this.dummy = DummyUnitStack.get()
				..setPos(pos)
				..setFlyHeight(100., 0.)
			this.mdl = this
				.dummy
				// .addEffect(Abilities.spiritOfVengeanceMissile, "origin")
				.addEffect(Abilities.cloudOfFog, "origin")

			this.vel = ZERO2.polarOffset(ang, GEN_VELOCITY * ANIMATION_PERIOD)
			this.duration = (SHROUD_RADIUS - EFFECT_RADIUS) / GEN_VELOCITY

	constant INVIS_ID  = 'A00L'
	constant BUF_ID    = 'B004'
	constant DURATION = 15.
	ShroudAbility array shrouds
	int shroudsIndex = -1
	ShroudEffect array gens
	int gensIndex = -1
	timer clock =  CreateTimer()
	timer clock2 = CreateTimer()


	function p2()
		int index = 0
		ShroudEffect g
		profile(gensIndex)
		while index <= gensIndex
			g = gens[index]

			g.duration -= ANIMATION_PERIOD
			g.dummy.setXY(g.dummy.getPos() + g.vel)

			if g.duration <= 0.
				destroy g

				gens[index] = gens[gensIndex]
				gensIndex--
				index--
				if gensIndex == -1
					PauseTimer(clock2)

			index++

	function p()
		var index = 0
		while index <= shroudsIndex
			let s = shrouds[index]
			s.time = s.time + ANIMATION_PERIOD
			s.ticks++

			if s.ticks % 10 == 0
				let g = new ShroudEffect(
					s.pos,
					angle(s.time * 8.)
				)

				gensIndex++
				gens[gensIndex] = g
				if gensIndex == 0
					TimerStart(clock2, ANIMATION_PERIOD, true, function p2)

			if s.caster.getPos().distanceToSq(s.pos) < (SHROUD_RADIUS * SHROUD_RADIUS)
				if GetUnitAbilityLevel(s.caster, BUF_ID) < 1
					InstantDummyCaster.castTarget(
						Player(PLAYER_NEUTRAL_PASSIVE),
						INVIS_ID,
						1,
						"invisibility",
						s.caster,
						s.caster.getPos()
					)
			else
				UnitRemoveAbility(s.caster, BUF_ID)

			if not s.caster.isAlive() or GetUnitTypeId(s.caster) == 0 or s.time > DURATION
				destroy s
				shrouds[index] = shrouds[shroudsIndex]
				shroudsIndex--
				index--
				if shroudsIndex == -1
					PauseTimer(clock)

			index++

	function c()
		if GetSpellAbilityId() == ID_SHROUD
			shroudsIndex++
			shrouds[shroudsIndex] = new ShroudAbility(GetTriggerUnit())
			if shroudsIndex == 0
				TimerStart(clock, ANIMATION_PERIOD, true, function p)

	init
		registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, function c)
