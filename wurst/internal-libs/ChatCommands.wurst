package ChatCommands
import Damage
import Equipment
import LinkedList
import Muscii
import Profile
import Ships
import SinglePlayer
import WurstCommand

init
	addCommand("music") (p, _cmd, _args) ->
		p.setWantsMusic(not p.wantsMusic())
	addCommand("help") (p, _cmd, _args) ->
		p.print("|cffffcc00Objective:|r destroy the enemy team's Keep.")
		p.print("|cffffcc00Crewmembers:|r hired from your Keep.  Automatically attack enemies nearby your helicopter.")
		p.print("|cffffcc00Equipment:|r bought from your Utility Port.  Can be sold for 100% their cost.")
	addCommand("clear") (p, _c, _a) ->
		if localPlayer == p
			ClearTextMessages()
	addCommand("reduction") (p, _c, _a) ->
		p.print(
			"|cffffcc00Damage Reduction:|r " +
			(
				Equipment.reduction.has(Ships.ships[p.getId()]) ?
				Equipment.reduction.get(Ships.ships[p.getId()]).toString() :
				"0"
			) +
			"%"
		)
	addCommand("stats") (p, _c, _a) ->
		Stats.display(p)
	if profiling_enabled
		addCommand("profile") (p, _c, _a) ->
			Profile.display(p)
	if SinglePlayer.isSinglePlayer
		addCommand("experience") (_p, _c, _a) ->
			SinglePlayer.experience()
		addCommand("help") (_p, _c, _a) ->
			SinglePlayer.help()
		addCommand("visibility") (_p, _c, _a) ->
			SinglePlayer.visibility()
		addCommand("fast") (_p, _c, _a) ->
			SinglePlayer.fast()
		addCommand("heal") (_p, _c, _a) ->
			SinglePlayer.heal()
		addCommand("hp") (_p, _c, a) ->
			SinglePlayer.hp(a)
		addCommand("damage") (_p, _c, a) ->
			SinglePlayer.damage(a)
