package ChatCommands
import Damage
import Equipment
import Muscii
import Ships

import WurstCommand
import WurstProfile
import WurstAllocationHistogram


init
	addCommand("music") (p, _cmd, _args) ->
		p.setWantsMusic(not p.wantsMusic())
	addCommand("help") (p, _cmd, _args) ->
		p.print("|cffffcc00Objective:|r destroy the enemy team's Keep.")
		p.print("|cffffcc00Crewmembers:|r hired from your Keep.  Automatically attack enemies nearby your helicopter.")
		p.print("|cffffcc00Equipment:|r bought from your Utility Port.  Can be sold for 100% their cost.")
	addCommand("clear") (p, _c, _a) ->
		if localPlayer == p
			ClearTextMessages()
	addCommand("speed") (p, _c, _a) ->
		printTimedToPlayer(
			Ships.ships[p.getId()].getMoveSpeed().toString(1),
			10.,
			p
		)
	addCommand("reduction") (p, _c, _a) ->
		p.print(
			"|cffffcc00Damage Reduction:|r " +
			(
				Equipment.reduction.has(Ships.ships[p.getId()]) ?
				Equipment.reduction.get(Ships.ships[p.getId()]).toString() :
				"0"
			) +
			"%"
		)
	addCommand("stats") (p, _c, _a) ->
		Stats.display(p)
	addCommand("reflection") (p, _c, _a) ->
		reflection_summary(p)
	if profiling_enabled
		addCommand("profile") (p, _c, _a) ->
			Profile.display(p)
