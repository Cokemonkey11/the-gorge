package AbilitiesApi

import AbilityObjEditing
import Assets
import LinkedList
import ObjectIds
import WurstLinkedHashMap

public constant ID_SPEED_BONUS_1     = 'A00C'
public constant ID_SPEED_BONUS_2     = 'A!00'
public constant ID_SPEED_BONUS_3     = 'A!01'
public constant ID_SPEED_BONUS_4     = 'A!02'

public constant SPEED_BONUS_1 =  50
public constant SPEED_BONUS_2 =  75
public constant SPEED_BONUS_3 = 100
public constant SPEED_BONUS_4 = 125

public constant ID_HEALTH_REGEN_BONUS_1     = 'A00E'
public constant ID_HEALTH_REGEN_BONUS_2     = 'A!03'
public constant ID_HEALTH_REGEN_BONUS_3     = 'A!04'
public constant ID_HEALTH_REGEN_BONUS_4     = 'A!05'

public constant HEALTH_REGEN_BONUS_1 =  8
public constant HEALTH_REGEN_BONUS_2 = 12
public constant HEALTH_REGEN_BONUS_3 = 16
public constant HEALTH_REGEN_BONUS_4 = 20

public constant ID_HERO_GLOW           = 'Af00'
public constant ID_SELECT_UNIT         = 'AE01'
public constant ID_FIRE_CREWMEMBER     = 'A00M'
public constant ID_SHROUD              = 'A00Y'

public constant ID_MAGNETIC_REACTORS     = 'Az00'
public constant ID_REGENERATOR           = 'Az01'
public constant ID_MONSOON_MK1           = 'Az02'
public constant ID_SEND_GOLD             = 'Az03'
public constant ID_XTS_MISSILE_ARRAY     = 'Az04'
public constant ID_EMP                   = 'Az05'
public constant ID_ION_CANNONS           = 'Az06'
public constant ID_DISRUPTOR             = 'Az07'
public constant ID_FIREBLAST             = 'Az08'
public constant ID_FLIGHT_MODE           = 'Az09'
public constant ID_FLIGHT_MODE_OFF       = 'Az0A'

public constant ID_SHOW_TRADE_EXP        = 'A00N'

public class CrewmemberProps
    int id
    int idpp
    int idcargo
    string name
    int price
    string missile
    int damage
    real aps
    int range
    int posX
    int posY
    string icon
    real projectileScale
    string soundSet
    string dummyModel
    real dummyScale
    int projectileSpeed
    bool shouldHitStructures
    real projectileArc
    int idIcon

    static function regressDamage(real aps, int range, int cost) returns real
        return (-0.10868*range + 0.26748*cost + 98.35081) / aps

        // Old damage values:
        //  25
        //  13
        //  45
        // 123
        //  70
        //  50
        //  30
        // 115
        //  50

        // Regression data:
        // 450   20   25     hh huge buff
        // 500   20   26     dok huge buff
        // 900   20   22     tusk huge nerf
        // 200   20   61     unbroken buff
        // 650   150  77     shaman nerf
        // 900   150  55     grenadier nerf
        // 200   150  168    lr signif nerf
        // 800   600  161    grph buff
        // 650   600  187.5  archer fine
        // 1000  600  145    temptress slight buff

    construct(int id, int idpp, int idcargo, string name, int posX, int posY, int price, string missile, real aps, int range, string icon, real projectileScale, string soundSet, string dummyModel, real dummyScale, int projectileSpeed, bool shouldHitStructures, real projectileArc, int idIcon)
        this.id = id            // The purchasable unit.  No model.  Always stacks on hero.
        this.idpp = idpp        // The phoenix fire ability.  Attachs to the purchasable unit.
        this.idcargo = idcargo  // The dummy unit that appears in the player's cargo hold.  Acts as a handle for selling.
        this.name = name
        this.price = price
        this.missile = missile
        // Used multiple linear regression - may need some work.
        this.damage = regressDamage(aps, range, price).toInt()
        this.aps = aps  // Attacks per second.
        this.range = range
        this.posX = posX
        this.posY = posY
        this.icon = icon
        this.projectileScale = projectileScale
        this.soundSet = soundSet
        this.dummyModel = dummyModel
        this.dummyScale = dummyScale
        this.projectileSpeed = projectileSpeed
        this.shouldHitStructures = shouldHitStructures
        this.projectileArc = projectileArc
        this.idIcon = idIcon  // The ID for the ability that cargo dummies have to provide information.

public enum Crewmember
    None
    Headhunter
    WitchDoctor
    TuskarrSpearman
    Unbroken
    Shaman
    Grenadier
    LaserRifleman
    GryphonRider
    VileTemptress
    Warden
    ShadowHunter
    Priestess
    TaurenChieftan

public constant ID_SEAWITCH       = 'o005'
public constant ID_SEAWITCH_D     = 'o00B'
public constant ID_SEAWITCH_PP    = 'A005'
public constant ID_SEAWITCH_ICON  = '![09'

public constant ID_AMASTER        = 'o00I'
public constant ID_AMASTER_D      = 'o00J'
public constant ID_AMASTER_PP     = asList('A00S', 'A00T', 'A00U')
public constant ID_AMASTER_ICON   = '![0A'

public constant ID_ODRAKE         = 'o00K'
public constant ID_ODRAKE_D       = 'o00L'
public constant ID_ODRAKE_PP      = 'A00V'
public constant ID_ODRAKE_ICON    = '![0B'

public constant CREW_PROPS = new LinkedHashMap<Crewmember, CrewmemberProps>()
    ..put(Crewmember.Headhunter,      new CrewmemberProps('o000', 'A000', 'o009', "Headhunter",            0, 0,   20, Abilities.huntermissile,       /* Attackspeed */ 1.,    400, Icons.bTNHeadhunter,          /* Projectile Scale */ 1.,  "HeadHunter",         Units.headhunter_V1,        /* Cargodummy Scale */ .6, /* Projectile Speed */ 1000, true,   .1,  '![00'))
    ..put(Crewmember.WitchDoctor,     new CrewmemberProps('o001', 'A003', 'o00F', "Witch Doctor",          1, 0,   20, Abilities.witchDoctorMissile,  /* Attackspeed */  .9,   650, Icons.bTNWitchDoctor,         /* Projectile Scale */  .5, "WitchDoctor",        Units.witchDoctor1,         /* Cargodummy Scale */ .6, /* Projectile Speed */ 1000, true,  0.,   '![01'))
    ..put(Crewmember.TuskarrSpearman, new CrewmemberProps('o002', 'A001', 'o00D', "Tuskarr Spearman",      2, 0,   20, Abilities.tuskarSpear,         /* Attackspeed */  .5,   900, Icons.bTNTuskaarBrown,        /* Projectile Scale */ 1.,  "Tuskarr",            Units.tuskarRanged,         /* Cargodummy Scale */ .5, /* Projectile Speed */ 1700, false,  .1,  '![02'))
    ..put(Crewmember.Unbroken,        new CrewmemberProps('o003', 'A002', 'o00E', "Unbroken",              3, 0,   20, Abilities.wardenMissile,       /* Attackspeed */  .5,   200, Icons.bTNUnbroken,            /* Projectile Scale */ 1.,  "Unbroken",           Units.unbroken,             /* Cargodummy Scale */ .4, /* Projectile Speed */ 1000, true,  0.,   '![03'))
    ..put(Crewmember.LaserRifleman,   new CrewmemberProps('o007', 'A007', 'o00A', "Laser Rifleman",        0, 1,  150, Abilities.phoenix_Missile,     /* Attackspeed */ 5.,    400, Icons.bTNSkink,               /* Projectile Scale */  .3, "Skink",              Units.skink1,               /* Cargodummy Scale */ .9, /* Projectile Speed */ 1500, true,  0.,   '![06'))
    ..put(Crewmember.Shaman,          new CrewmemberProps('o004', 'A004', 'o00C', "Shaman",                1, 1,  150, Abilities.farseerMissile,      /* Attackspeed */ 1.1,   650, Icons.bTNShaman,              /* Projectile Scale */  .8, "Shaman",             Units.shaman1,              /* Cargodummy Scale */ .6, /* Projectile Speed */ 1200, true,   .15, '![04'))
    ..put(Crewmember.Grenadier,       new CrewmemberProps('o006', 'A006', 'o008', "Grenadier",             2, 1,  150, Abilities.rocketMissile,       /* Attackspeed */  .8,   900, Icons.bTNMortarTeam,          /* Projectile Scale */  .6, "Grenadier",          Units.mortarTeam1,          /* Cargodummy Scale */ .6, /* Projectile Speed */ 1200, false,  .15, '![05'))
    ..put(Crewmember.GryphonRider,    new CrewmemberProps('o00G', 'A00Q', 'o00H', "Gryphon Rider",         0, 0,  600, Abilities.gryphonRiderMissile, /* Attackspeed */ 1.43,  400, Icons.bTNGryphonRider,        /* Projectile Scale */  .7, "GryphonRider",       Units.gryphonRider1,        /* Cargodummy Scale */ .6, /* Projectile Speed */ 1600, true,   .1,  '![07'))
    ..put(Crewmember.VileTemptress,   new CrewmemberProps('o00M', 'A00X', 'o00N', "Vile Temptress",        2, 0,  600, Abilities.necromancerMissile,  /* Attackspeed */ 2.,    900, Icons.bTNDemoness,            /* Projectile Scale */  .8, "Demoness",           Units.demoness1,            /* Cargodummy Scale */ .7, /* Projectile Speed */ 1600, false, 0.,   '![08'))
    ..put(Crewmember.Warden,          new CrewmemberProps('oP00', 'AP00', 'oC00', "Warden",                0, 1, 2500, Abilities.fanOfKnivesMissile,  /* Attackspeed */ 2.,    400, Icons.bTNWarden2,             /* Projectile Scale */  .8, "HeroWarden",         Units.heroWarden,           /* Cargodummy scale */ .5, /* Projectile Speed */ 1400, true,  0.,   '![0C'))
    ..put(Crewmember.ShadowHunter,    new CrewmemberProps('oP01', 'AP01', 'oC01', "Shadow Hunter",         1, 1, 2500, Abilities.shadowHunterMissile, /* Attackspeed */ 1.1,   650, Icons.bTNShadowHunter,        /* Projectile Scale */  .7, "HeroShadowHunter",   Units.heroShadowHunter1,    /* Cargodummy scale */ .6, /* Projectile Speed */ 1000, true,   .15, '![0D'))
    ..put(Crewmember.Priestess,       new CrewmemberProps('oP02', 'AP02', 'oC02', "Priestess of the Moon", 2, 1, 2500, Abilities.sentinelMissile,     /* Attackspeed */  .7,   900, Icons.bTNPriestessOfTheMoon,  /* Projectile Scale */ 1.,  "HeroMoonPriestess",  Units.heroMoonPriestess,    /* Cargodummy scale */ .4, /* Projectile Speed */ 1000, false,  .1,  '![0E'))
    ..put(Crewmember.TaurenChieftan,  new CrewmemberProps('oP03', 'AP03', 'oC03', "Tauren Chieftan",       3, 1, 2500, Abilities.warStompCaster,      /* Attackspeed */  .5,   200, Icons.bTNHeroTaurenChieftain, /* Projectile Scale */  .8, "HeroTaurenChieftan", Units.heroTaurenChieftain1, /* Cargodummy scale */ .3, /* Projectile Speed */ 2000, true,  0.,   '![0F'))


class MovespeedBonuses extends AbilityDefinitionMoveSpeedBonus
    construct(int id, int speed_bonus)
        super(id)
        setMovementSpeedBonus(1, speed_bonus)

@compiletime function initItemMovespeedAbilities()
    new MovespeedBonuses(ID_SPEED_BONUS_1, SPEED_BONUS_1)
    new MovespeedBonuses(ID_SPEED_BONUS_2, SPEED_BONUS_2)
    new MovespeedBonuses(ID_SPEED_BONUS_3, SPEED_BONUS_3)
    new MovespeedBonuses(ID_SPEED_BONUS_4, SPEED_BONUS_4)

class HealthregenBonuses extends AbilityDefinitionRegenLife
    construct(int id, int hpps_bonus)
        super(id)
        setHitPointsRegeneratedPerSecond(1, hpps_bonus)
        setArtTarget("")
        setArtCaster("")

@compiletime function initItemHealthregenAbilities()
    new HealthregenBonuses(ID_HEALTH_REGEN_BONUS_1, HEALTH_REGEN_BONUS_1)
    new HealthregenBonuses(ID_HEALTH_REGEN_BONUS_2, HEALTH_REGEN_BONUS_2)
    new HealthregenBonuses(ID_HEALTH_REGEN_BONUS_3, HEALTH_REGEN_BONUS_3)
    new HealthregenBonuses(ID_HEALTH_REGEN_BONUS_4, HEALTH_REGEN_BONUS_4)

class AbilHeroGlow extends AbilityDefinitionNeutralRegenhealthonly
    construct(int id)
        super(id)
        setPercentage(1, false)
        setAmountofHitPointsRegenerated(1, 0)
        setArtTarget("GeneralHeroGlow.mdx")
        setTargetAttachmentPoint("origin")
        setTargetsAllowed(1, "self")
        setName("heroglow")

@compiletime function initHeroGlow()
    new AbilHeroGlow(ID_HERO_GLOW)

class SelectUnitAbil extends AbilityDefinitionNeutralBuildinganyunit
    construct(int id)
        super(id)
        setCastRange(1, 450.)

@compiletime function initSelectUnit()
    new SelectUnitAbil(ID_SELECT_UNIT)

enum TargetType
    Instant
    Unit
    Point

function TargetType.toChannelIntValue() returns int
    switch this
        case Instant
            return 0
        case Unit
            return 1
        case Point
            return 2

class ChannelCommon extends AbilityDefinitionIllidanChannel
    construct(int id, string animationName, int x, int y, string icon, real cooldown, string orderid, real castRange, int options, TargetType targetType, string hotkey, string name, string targetsAllowed, string tooltipExtended, real followThroughTime, real aoe)
        super(id)
        setAnimationNames(animationName)
        setButtonPositionNormalX(x)
        setButtonPositionNormalY(y)
        setArtCaster("")
        setCasterAttachmentPoint("")
        setArtEffect("")
        setIconNormal(icon)
        setArtTarget("")
        setCooldown(1, cooldown)
        setTargetAttachmentPoint("")
        setBaseOrderID(1, orderid)
        setDisableOtherAbilities(1, false)
        setFollowThroughTime(1, followThroughTime)
        setOptions(1, options)
        setCastRange(1, castRange)
        setTargetType(1, targetType.toChannelIntValue())
        setHeroAbility(false)
        setLevels(1)
        setCheckDependencies(false)
        setHotkeyNormal(hotkey)
        setName(name)
        setTargetsAllowed(1, targetsAllowed)
        setOrderStringUseTurnOn(orderid)
        setTooltipNormal(1, name + " [|cffffcc00" + hotkey + "|r]")
        setTooltipNormalExtended(1, tooltipExtended)
        setAreaofEffect(1, aoe)  // Used for targeting image.

@compiletime function init_xts()
    new ChannelCommon(
        ID_XTS_MISSILE_ARRAY,
        "spell",
        0,
        2,
        Icons.bTNScatterRockets,
        30.,
        "acidbomb",
        1100.,
        1,
        TargetType.Unit,
        "Q",
        "XTS Missile Barrage",
        "air,enemies,ground,vulnerable",
        "A barrage of 10 missiles is launched in sequence.  Each one seeks out the target enemy unit.|n |n|cffffcc00Damage (Each):|r 55|n|cffffcc00Cooldown:|r 30s|n|cffffcc00Range:|r 1100",
        0.,
        0.
    )

@compiletime function init_ion_cannons()
    new ChannelCommon(
        ID_ION_CANNONS,
        "spell",
        0,
        2,
        Icons.bTNBlizzard,
        15.,
        "acidbomb",
        1000.,
        1,
        TargetType.Point,
        "Q",
        "Ion Cannons",
        "air,enemies,ground,vulnerable",
        "Begins channeling to divert power.  Ion bolts are loosed periodically in the target direction.|n |n|cffffcc00Damage (Each):|r 100|n|cffffcc00Cooldown:|r 15s|n|cffffcc00Range:|r 1100|n|cffffcc00Rate of fire:|r 2 per second",
        10.,
        0.)

@compiletime function init_emp()
    new ChannelCommon(
        ID_EMP,
        "spell",
        1,
        2,
        Icons.bTNLightningShield,
        40.,
        "ancestralspirit",
        300.,
        1,
        TargetType.Instant,
        "W",
        "EMP",
        "air,enemies,ground,vulnerable",
        "While channeling, a short wavelength signal is emitted.  If an enemy hero is struck, a secondary effect is triggered: a barrage of plasma is emitted which damages and stuns the marked unit. While this effect occurs, the marked enemy deals reduced damage to you. Both effects are interrupted if channeling is ceased.|n |n|cffffcc00Range:|r 250|n|cffffcc00Damage:|r 150|n|cffffcc00Duration:|r 5s|n|cffffcc00Cooldown:|r 40s|n|cffffcc00Damage Reduction:|r 20%",
        5. + 1.,
        0.
    )

@compiletime function initFire()
    new ChannelCommon(
        ID_FIRE_CREWMEMBER,
        "spell",
        0,
        2,
        Icons.bTNChestOfGold,
        20.,
        "acidbomb",
        800.,
        1,
        TargetType.Instant,
        "Q",
        "Fire Crewmember",
        "hero,allied,invulnerable,vulnerable,notself",
        "Unemploy this crewmember.  Returns half their cost as gold.",
        0.,
        0.
    )

@compiletime function initSendGold()
    new ChannelCommon(
        ID_SEND_GOLD,
        "spell",
        1,
        2,
        Icons.bTNChestOfGold,
        20.,
        "acidbomb",
        800.,
        1,
        TargetType.Unit,
        "W",
        "Send Gold",
        "hero,allied,invulnerable,vulnerable,notself",
        "Send your own gold to an allied player ship.  The recipient gets free interest that increases with your trading experience.|n|n|cffffcc00Maximum Base Gold:|r 500|n|cffffcc00Interest Rate:|r 1% for every 1 trading experience",
        0.,
        0.
    )

@compiletime function initMagneticReactors()
    new ChannelCommon(
        ID_MAGNETIC_REACTORS,
        "spell",
        1,
        2,
        Icons.bTNBash,
        30.,
        "ancestralspirit",
        384.,
        1,
        TargetType.Instant,
        "W",
        "Magnetic Reactors",
        "ground,invulnerable,neutral",
        "Restores a fraction of your missing health over time.|n |n|cffffcc00Missing Health Restored:|r 15%|n|cffffcc00Heal Duration:|r 5s|n|cffffcc00Cooldown:|r 30s",
        0.,
        0.
    )

@compiletime function initRegenerator()
    new ChannelCommon(
        ID_REGENERATOR,
        "spell",
        1,
        2,
        Icons.bTNArcaneVault,
        40.,
        "ancestralspirit",
        384.,
        1,
        TargetType.Instant,
        "W",
        "Regenerator",
        "ground,invulnerable,neutral",
        "Diverts engine power to regenerate health while channeling.|n |n|cffffcc00Health Restored:|r 30 per second|n|cffffcc00Channel Duration:|r 10s|n|cffffcc00Cooldown:|r 40s",
        10.,
        0.
    )

@compiletime function initMonsoonMk1Abil()
    new ChannelCommon(
        ID_MONSOON_MK1,
        "spell",
        1,
        2,
        Icons.bTNMonsoon,
        30.,
        "ancestralspirit",
        400.,
        1 + 2, // Visible + Targeting Image
        TargetType.Point,
        "W",
        "Monsoon Mk.1",
        "air,enemies,ground,vulnerable,terrain",
        "Marks a group of units and begins building power. After a time, the power is unleashed, and units that are still marked, in range, are damaged.|n |n|cffffcc00Damage:|r 250|n|cffffcc00Charge Duration:|r 3.5s|n|cffffcc00Cooldown:|r 30s|n|cffffcc00Range:|r 400",
        0.,
        200.
    )

@compiletime function init_disruptor_beam()
    new ChannelCommon(
        ID_DISRUPTOR,
        "spell",
        1,
        2,
        Icons.bTNSilence,
        30.,
        "ancestralspirit",
        1200.,
        1,
        TargetType.Point,
        "W",
        "Disruptor Beam",
        "enemies,ground,vulnerable",
        "Charges breifly before emitting an enormous beam that silences enemy units.|n |n|cffffcc00Range:|r 1200|n|cffffcc00Duration:|r 5s|n|cffffcc00Cooldown:|r 30s|n|cffffcc00Beam width:|r 400",
        .5,
        0.
    )

@compiletime function init_fire_blast()
    new ChannelCommon(
        ID_FIREBLAST,
        "",
        0,
        2,
        Icons.bTNVolcano,
        30.,
        "acidbomb",
        900.,
        1,
        TargetType.Point,
        "Q",
        "Fire Blast",
        "enemies,ground,vulnerable",
        "Looses a triplet of molten fire bombs in the target direction.  Damage scales with movement speed, including speed obtained in flight mode.|n |n|cffffcc00Damage:|r 100 + 50% ms|n|cffffcc00Range:|r 900|n|cffffcc00Cooldown:|r 30",
        0.,
        0.
    )

@compiletime function init_flight_mode()
    new ChannelCommon(
        ID_FLIGHT_MODE,
        "",
        1,
        2,
        Icons.bTNWindWalkOn,
        0.,
        "ancestralspirit",
        0.,
        1,
        TargetType.Instant,
        "W",
        "Flight Mode",
        "enemies,ground,vulnerable",
        "Toggleable.  Provides an alternate movement style with the capability to move at much greater speed, at a cost of reduced control.|n |n|cffffcc00Maximum speed:|r 200% ms",
        0.,
        0
    )

    new ChannelCommon(
        ID_FLIGHT_MODE_OFF,
        "",
        1,
        2,
        Icons.bTNWindWalkOff,
        0.,
        "ancestralspirit",
        0.,
        1,
        TargetType.Instant,
        "W",
        "Hover Mode",
        "enemies,ground,vulnerable",
        "Disables flight mode.",
        0.,
        0.
    )

@compiletime function create_w3a_A00N()
    let _def = createObjectDefinition("w3a", ID_SHOW_TRADE_EXP, 'ANcl')
        ..setLvlDataString("aani", 0, 0, "spell")
        ..setLvlDataInt("abpx", 0, 0, 0)
        ..setLvlDataInt("aher", 0, 0, 0)
        ..setLvlDataInt("alev", 0, 0, 1)
        ..setLvlDataString("acat", 0, 0, "")
        ..setLvlDataString("acap", 0, 0, "")
        ..setLvlDataString("aeat", 0, 0, "")
        ..setLvlDataString("aart", 0, 0, "ReplaceableTextures\\CommandButtons\\BTNMagicVault.blp")
        ..setLvlDataString("atat", 0, 0, "")
        ..setLvlDataString("Ncl6", 1, 6, "acolyteharvest")
        ..setLvlDataUnreal("Ncl1", 1, 1, 0.0)
        ..setLvlDataInt("Ncl3", 1, 3, 1)
        ..setLvlDataUnreal("aran", 1, 0, 0.0)
        ..setLvlDataString("arac", 0, 0, "orc")
        ..setLvlDataInt("achd", 0, 0, 0)
        ..setLvlDataString("ahky", 0, 0, "Q")
        ..setLvlDataString("anam", 0, 0, "Trading Experience")
        ..setLvlDataString("aord", 0, 0, "acolyteharvest")
        ..setLvlDataString("atp1", 1, 0, "Show Trading Experience [|cffffcc00Q|r]")
        ..setLvlDataString("aub1", 1, 0, "Displays your captain's trading experience.")
        ..setLvlDataInt("abpy", 0, 0, 2)
        ..setLvlDataUnreal("acdn", 1, 0, 0.5)
        ..setLvlDataString("ata0", 0, 0, "")
        ..setLvlDataUnreal("Ncl4", 1, 4, 0.0)
        ..setLvlDataInt("Ncl5", 1, 5, 0)

class PhoenixFireBase extends AbilityDefinitionPhoenixFire
    construct(int abilId, string name, string missile, int missileSpeed, real damage, real range, real cooldown, bool hitStructures, real missileArc)
        super(abilId)
        setName(name)
        setEditorSuffix("(Phoenix Fire)")
        setMissileArt(missile)
        setMissileSpeed(missileSpeed)
        setInitialDamage(1, damage)
        setAreaofEffect(1, range)
        setDurationHero(1, .01)
        setDurationNormal(1, .01)
        setCooldown(1, cooldown)
        setTargetsAllowed(1, "air,enemies,ground,vulnerable" + (hitStructures ? ",structure" : ""))
        setMissileArc(missileArc)

@compiletime function create_phoenix_fires()
    CREW_PROPS.forEach() (LHMEntry<Crewmember, CrewmemberProps> kv) ->
        new PhoenixFireBase(
            kv.value.idpp,
            kv.value.name,
            kv.value.missile,
            kv.value.projectileSpeed,
            kv.value.damage.toReal(),
            kv.value.range.toReal(),
            1. / kv.value.aps,
            kv.value.shouldHitStructures,
            kv.value.projectileArc
        )

@compiletime function create_w3a_A005()
    let _def = createObjectDefinition("w3a", 'A005', 'Apxf')
        ..setLvlDataString("anam", 0, 0, "Sea Witch")
        ..setLvlDataString("ansf", 0, 0, "(Phoenix Fire)")
        ..setLvlDataString("atp1", 1, 0, "")
        ..setLvlDataString("aub1", 1, 0, "")
        ..setLvlDataInt("abpy", 0, 0, 0)
        ..setLvlDataString("aart", 0, 0, "")
        ..setLvlDataString("amat", 0, 0, "Abilities\\Spells\\Other\\FrostArrows\\NagaColdArrowMissile.mdl")
        ..setLvlDataUnreal("pxf2", 1, 2, 0.0)
        ..setLvlDataUnreal("pxf1", 1, 1, 35.0)
        ..setLvlDataUnreal("aare", 1, 0, 550.0)
        ..setLvlDataUnreal("ahdu", 1, 0, 0.01)
        ..setLvlDataUnreal("adur", 1, 0, 0.01)
        ..setLvlDataUnreal("acdn", 1, 0, 0.8)
        ..setLvlDataString("atar", 1, 0, "air,enemies,ground,structure,vulnerable")
        ..setLvlDataUnreal("amac", 0, 0, 0.1)

@compiletime function create_w3a_A00S()
    let _def = createObjectDefinition("w3a", 'A00S', 'Apxf')
        ..setLvlDataString("anam", 0, 0, "Master Archer")
        ..setLvlDataString("ansf", 0, 0, "(A) (Phoenix Fire)")
        ..setLvlDataString("atp1", 1, 0, "")
        ..setLvlDataString("aub1", 1, 0, "")
        ..setLvlDataInt("abpy", 0, 0, 0)
        ..setLvlDataString("aart", 0, 0, "")
        ..setLvlDataString("amat", 0, 0, "Abilities\\Weapons\\GuardTowerMissile\\GuardTowerMissile.mdl")
        ..setLvlDataInt("amsp", 0, 0, 1800)
        ..setLvlDataUnreal("pxf2", 1, 2, 0.0)
        ..setLvlDataUnreal("pxf1", 1, 1, 50.0)
        ..setLvlDataUnreal("aare", 1, 0, 650.0)
        ..setLvlDataUnreal("ahdu", 1, 0, 0.01)
        ..setLvlDataUnreal("adur", 1, 0, 0.01)
        ..setLvlDataUnreal("acdn", 1, 0, 0.8)
        ..setLvlDataString("atar", 1, 0, "air,enemies,ground,structure,vulnerable")
        ..setLvlDataUnreal("amac", 0, 0, 0.1)
        ..setLvlDataInt("amho", 0, 0, 0)


@compiletime function create_w3a_A00T()
    let _def = createObjectDefinition("w3a", 'A00T', 'Apxf')
        ..setLvlDataString("anam", 0, 0, "Master Archer")
        ..setLvlDataString("ansf", 0, 0, "(B) (Phoenix Fire)")
        ..setLvlDataString("atp1", 1, 0, "")
        ..setLvlDataString("aub1", 1, 0, "")
        ..setLvlDataInt("abpy", 0, 0, 0)
        ..setLvlDataString("aart", 0, 0, "")
        ..setLvlDataString("amat", 0, 0, "Abilities\\Weapons\\GuardTowerMissile\\GuardTowerMissile.mdl")
        ..setLvlDataInt("amsp", 0, 0, 1800)
        ..setLvlDataUnreal("pxf2", 1, 2, 0.0)
        ..setLvlDataUnreal("pxf1", 1, 1, 50.0)
        ..setLvlDataUnreal("aare", 1, 0, 650.0)
        ..setLvlDataUnreal("ahdu", 1, 0, 0.01)
        ..setLvlDataUnreal("adur", 1, 0, 0.01)
        ..setLvlDataUnreal("acdn", 1, 0, 0.8)
        ..setLvlDataString("atar", 1, 0, "air,enemies,ground,structure,vulnerable")
        ..setLvlDataUnreal("amac", 0, 0, 0.1)
        ..setLvlDataInt("amho", 0, 0, 0)


@compiletime function create_w3a_A00U()
    let _def = createObjectDefinition("w3a", 'A00U', 'Apxf')
        ..setLvlDataString("anam", 0, 0, "Master Archer")
        ..setLvlDataString("ansf", 0, 0, "(C) (Phoenix Fire)")
        ..setLvlDataString("atp1", 1, 0, "")
        ..setLvlDataString("aub1", 1, 0, "")
        ..setLvlDataInt("abpy", 0, 0, 0)
        ..setLvlDataString("aart", 0, 0, "")
        ..setLvlDataString("amat", 0, 0, "Abilities\\Weapons\\GuardTowerMissile\\GuardTowerMissile.mdl")
        ..setLvlDataInt("amsp", 0, 0, 1800)
        ..setLvlDataUnreal("pxf2", 1, 2, 0.0)
        ..setLvlDataUnreal("pxf1", 1, 1, 50.0)
        ..setLvlDataUnreal("aare", 1, 0, 650.0)
        ..setLvlDataUnreal("ahdu", 1, 0, 0.01)
        ..setLvlDataUnreal("adur", 1, 0, 0.01)
        ..setLvlDataUnreal("acdn", 1, 0, 0.8)
        ..setLvlDataString("atar", 1, 0, "air,enemies,ground,structure,vulnerable")
        ..setLvlDataUnreal("amac", 0, 0, 0.1)
        ..setLvlDataInt("amho", 0, 0, 0)


@compiletime function create_w3a_A00V()
    let _def = createObjectDefinition("w3a", 'A00V', 'Apxf')
        ..setLvlDataString("anam", 0, 0, "Obsidian Drake")
        ..setLvlDataString("ansf", 0, 0, "(Phoenix Fire)")
        ..setLvlDataString("atp1", 1, 0, "")
        ..setLvlDataString("aub1", 1, 0, "")
        ..setLvlDataInt("abpy", 0, 0, 0)
        ..setLvlDataString("aart", 0, 0, "")
        ..setLvlDataString("amat", 0, 0, "Abilities\\Weapons\\RedDragonBreath\\RedDragonMissile.mdl")
        ..setLvlDataUnreal("pxf2", 1, 2, 0.0)
        ..setLvlDataUnreal("pxf1", 1, 1, 320.0)
        ..setLvlDataUnreal("ahdu", 1, 0, 0.01)
        ..setLvlDataUnreal("adur", 1, 0, 0.01)
        ..setLvlDataUnreal("acdn", 1, 0, 1.8)
        ..setLvlDataString("atar", 1, 0, "air,enemies,ground,structure,vulnerable")
        ..setLvlDataInt("abpx", 0, 0, 2)
        ..setLvlDataInt("amsp", 0, 0, 1200)

class CrewmemberInfoIcon extends AbilityDefinitionTaurenChieftainEnduranceAura
    construct(int id, string name, int damage, real aps, int range, int sellValue, string special)
        super(id)
        setHeroAbility(false)
        setTooltipNormal(1, "Info (" + name + ")")
        setTooltipNormalExtended(
            1,
            "|cffffcc00Sell value:|r "
            + sellValue.toString()
            + "|n |n|cff999999Damage:|r "
            + damage.toString()
            + "|n|cff999999Rate of fire:|r "
            + aps.toString(1)
            + " per second"
            + "|n|cff999999Range:|r "
            + range.toString()
            + (special.length() > 0 ? "|n|cffffcc00Special:|r " + special : "")
        )
        setIconNormal(Icons.pASBTNEngineeringUpgrade)
        setButtonPositionNormalX(0)
        setButtonPositionNormalY(0)
        setArtCaster("")
        setArtEffect("")
        setArtTarget("")
        setArtSpecial("")
        setTargetsAllowed(1, "none")

@compiletime function create_info_icons()
    CREW_PROPS.forEach() (LHMEntry<Crewmember, CrewmemberProps> kv) ->
        new CrewmemberInfoIcon(
            kv.value.idIcon,
            kv.value.name,
            kv.value.damage,
            kv.value.aps,
            kv.value.range,
            (kv.value.price / 2.).toInt(),
            ""
        )

    new CrewmemberInfoIcon(ID_SEAWITCH_ICON, "Sea Witch",       35, 1. / .8,  550,  75, "Attacks slow the target by 20% for 5 seconds (non-stacking)")
    new CrewmemberInfoIcon(ID_AMASTER_ICON,  "Master Archer",   50, 1. / .8,  650, 300, "Fires 3 independent projectiles (can strike the same target)")
    new CrewmemberInfoIcon(ID_ODRAKE_ICON,   "Obsidian Drake", 320, 1. / 1.8, 600, 300, "Deals a total of 400 damage if the target is below 400 HP")

class SilenceSingle extends AbilityDefinitionFirelordSoulBurn
    construct(int id)
        super(id)
        setHeroAbility(false)
        setLevels(10)
        presetAttackSpeedReduction(lvl -> 0.)
        presetDamageAmount(lvl -> 0.)
        presetMovementSpeedReduction(lvl -> 0.)
        presetDamagePenalty(lvl -> 0.)
        presetDurationNormal(lvl -> lvl.toReal())
        presetDurationHero(lvl -> lvl.toReal())
        presetManaCost(lvl -> 0)
        presetAreaofEffect(lvl -> 0.)
        presetBuffs(lvl -> commaList('BNsi'))
        presetTargetsAllowed(lvl -> "enemies,vulnerable,neutral")

@compiletime function create_silence()
    new SilenceSingle('$si$')
