package SinglePlayer
    import DamageEvent
    import LinkedList
    import Players
    import Ships

    public class SinglePlayer
        static constant SP_PREFIX = "[|cffffcc00Single Player|r] "
        static constant TAB       = "    "

        static          player thePlayer
        static          bool   fastEnabled = false
        static          var    isSinglePlayer = false

        static function help()
            print(SP_PREFIX + "Available commands:")
            print(TAB + "-help: Display this help menu.")
            print(TAB + "-experience: Gives 500 trading experience.")
            print(TAB + "-heal: fully heal your helicopter.")
            print(TAB + "-visibility: Disables Fog of War and Black Mask.")
            print(TAB + "-fast: Enables instant ship movement.")


        static function heal()
            let u = Ships.ships[thePlayer.getId()]
            u.setHP(u.getMaxHP())


        static function hp(LinkedList<string> args)
            Ships.ships[thePlayer.getId()].setHP(args.getFirst().toReal())


        static function damage(LinkedList<string> args)
            let ship = Ships.ships[thePlayer.getId()]
            DamageEvent.setNextDamageFromCode()
            ship.damageTarget(ship, args.getFirst().toReal())


        static function experience()
            print(SP_PREFIX + "500 trading experience added.")
            Players.tradeExp[thePlayer.getId()] += 500


        static function visibility()
            print(SP_PREFIX + "Full visibility enabled.")
            FogMaskEnable(false)
            FogEnable(false)


        static function fast()
            print(SP_PREFIX + "Fast movement enabled.")

            // Fast movement trigger.
            trigger f = CreateTrigger()
            TriggerRegisterUnitEvent(f, Ships.ships[SinglePlayer.thePlayer.getId()], EVENT_UNIT_ISSUED_POINT_ORDER)
            TriggerAddCondition(f, Condition(() -> begin
                GetTriggerUnit()..setX(GetOrderPointX())..setY(GetOrderPointY())
            end))


        static function enable()
            isSinglePlayer = true
            print(SP_PREFIX + "Single player mode enabled.  Enter -help for a list of commands.")

            int i = 2
            while i <= 11

                if Players.playing[i]
                    thePlayer = players[i]
                    break

                i++
