package SinglePlayer
    import Players
    import Ships

    public class SinglePlayer
        static constant SP_PREFIX = "[|cffffcc00Single Player|r] "
        static constant TAB       = "    "

        static          player thePlayer
        static          bool   fastEnabled = false

        static function help() returns bool
            print(SP_PREFIX + "Available commands:")
            print(TAB + "-h, -help: Display this help menu.")
            print(TAB + "-c, -clear: Clear the on-screen text messages.")
            print(TAB + "-e, -experience: Gives 500 trading experience.")
            print(TAB + "-v, -visibility: Disables Fog of War and Black Mask.")
            print(TAB + "-f, -fast: Enables instant ship movement.")

            return false


        static function experience() returns bool
            print(SP_PREFIX + "500 trading experience added.")
            Players.tradeExp[thePlayer.getId()] += 500

            return false


        static function visibility() returns bool
            print(SP_PREFIX + "Full visibility enabled.")
            FogMaskEnable(false)
            FogEnable(false)

            return false


        static function fast()
            print(SP_PREFIX + "Fast movement enabled.")

            // Fast movement trigger.
            trigger f = CreateTrigger()
            TriggerRegisterUnitEvent(f, Ships.ships[SinglePlayer.thePlayer.getId()], EVENT_UNIT_ISSUED_POINT_ORDER)
            TriggerAddCondition(f, Condition(() -> begin
                GetTriggerUnit()..setX(GetOrderPointX())..setY(GetOrderPointY())
                return false
            end))


        static function clear() returns bool
            // Debug message redacted to avoid waste.
            ClearTextMessages()

            return false


        static function enable()
            print(SP_PREFIX + "Single player mode enabled.  Enter -help for a list of commands.")

            int i = 2
            while i <= 11

                if Players.playing[i]
                    thePlayer = Players.players[i]
                    break

                i++

            trigger help = CreateTrigger()
            help..registerPlayerChatEvent(thePlayer, "-h",    true)
                ..registerPlayerChatEvent(thePlayer, "-help", true)
                ..addCondition(Condition(function help))

            trigger experience = CreateTrigger()
            experience..registerPlayerChatEvent(thePlayer, "-e",               true)
                      ..registerPlayerChatEvent(thePlayer, "-experience",      true)
                      ..addCondition(Condition(function experience))

            trigger visibility = CreateTrigger()
            visibility..registerPlayerChatEvent(thePlayer, "-v",          true)
                      ..registerPlayerChatEvent(thePlayer, "-visibility", true)
                      ..addCondition(Condition(function visibility))

            trigger clear = CreateTrigger()
            clear..registerPlayerChatEvent(thePlayer, "-c",     true)
                 ..registerPlayerChatEvent(thePlayer, "-clear", true)
                 ..addCondition(Condition(function clear))

            trigger fast = CreateTrigger()
            fast..registerPlayerChatEvent(thePlayer, "-f",      true)
                ..registerPlayerChatEvent(thePlayer, "-fast",   true)
                ..addCondition(Condition(function fast))
