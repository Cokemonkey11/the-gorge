package SoundsLib
	import HashMap

	class Slop
		string snd

	public class SoundsLib
		static Table preloadDB

		private static function preloadAfter()
			timer time = GetExpiredTimer()
			Slop s = preloadDB.loadInt(time.getHandleId()) castTo Slop
			sound sd = CreateSound(s.snd, false, false, false, 12700, 12700, "")
			SetSoundVolume(sd, 1)
			StartSound(sd)
			KillSoundWhenDone(sd)
			destroy s
			DestroyTimer(time)


		static function preload(string str)
			timer time= CreateTimer()
			let s = new Slop
			s.snd = str
			preloadDB.saveInt(time.getHandleId(), s castTo int)
			TimerStart(time, .01, false, function SoundsLib.preloadAfter)


		static function play3D(string str, real pitch, real x, real y, real z)
			sound s = CreateSound(str, false, true, true, 12700, 12700, "")
			SetSoundPosition(s, x, y, z)
			SetSoundVolume(s, 127)
			SetSoundPitch(s, pitch)
			StartSound(s)
			KillSoundWhenDone(s)


		static function playForPlayer(string str, real pitch, player who)
			sound s = CreateSound(str, false, false, false, 12700, 12700, "")
			if GetLocalPlayer()== who
				SetSoundVolume(s, 127)
			else
				SetSoundVolume(s, 0)

			SetSoundPitch(s, pitch)
			StartSound(s)
			KillSoundWhenDone(s)


		static function play(string str, real pitch, int vol)
			let s = CreateSound(str, false, false, false, 12700, 12700, "")
			SetSoundVolume(s, vol)
			SetSoundPitch(s, pitch)
			StartSound(s)
			KillSoundWhenDone(s)

	init
		SoundsLib.preloadDB = new Table
