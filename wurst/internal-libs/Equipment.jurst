// Making a new trade route:
//   * Create necessary items in object editor (contract and anything else)
//   * Add all those new items to Equipment constants
//   * Add contract cost to Equipment.getContractCost
//   * Add the item to Equipment.classes (done in Equipment#init)
//   * Add necessary items to DestroyDroppedContracts.tradeItems (done in DestroyDroppedContracts#init)
//   * Fill in pawned items in TradeRewardsPawn#c as necessary
//   * Fill in remaining triggers as necessary

package Equipment
	import SimError
	import Ships
	import Game
	import Players
	import RegisterPlayerUnitEvent

	public class Equipment
		static constant integer CLASS_UNKNOWN    = -1
		static constant integer CLASS_ENGINE     =  0
		static constant integer CLASS_MECHANIC   =  1
		static constant integer CLASS_HULL       =  2
		static constant integer CLASS_SHIELDGEN  =  3
		static constant integer CLASS_TELEPORT   =  4
		static constant integer CLASS_SPECIAL    =  5
		static constant integer CLASS_CONTRACT   =  6

		static constant integer PAGE_1_2         = 'I003'
		static constant integer PAGE_2_1         = 'I004'
		static constant integer PAGE_2_3         = 'I006'
		static constant integer PAGE_3_2         = 'I007'
		static constant integer PAGE_3_4         = 'I00A'
		static constant integer PAGE_4_3         = 'I00B'
		static constant integer KPAGE_1_2        = 'I00E'
		static constant integer KPAGE_2_1        = 'I00F'

		static constant integer DATA_ID          = 'I00D'
		static constant integer TOXIC_SUBST_FULL = 'I00H'
		static constant integer VENOM_VIAL       = 'I00J'

		// Engines
		static constant integer STEAM_CORE     = 'I000'
		// Mechanics
		static constant integer PROBLEM_SOLVER = 'I001'
		// Hulls
		static constant integer STONE_HULL     = 'I002'
		// Shield Generators
		static constant integer BRONZE_GEN     = 'I005'
		// Teleporters
		static constant integer IONIC_TELEPORT = 'I008'
		// Special
		static constant integer SHIP_LOCATOR   = 'I009'
		// Contracts
		static constant integer DATA_ACQUISIT         = 'I00C'
		static constant integer TOXIC_SUBST           = 'I00G'
		static constant integer SPIDER_VENOM_CONTRACT = 'I00I'

		// Hull reduction levels
		static constant integer REDUCTION1 = 10

		static constant string WRONG_CLASS             = "Your ship class cannot use that type of equipment."
		static constant string INSUFFICIENT_EXPERIENCE = "You have insufficient experience for the selected contract."

		static constant real EQUIPMENT_SELL_VALUE = 1.

		static Table classes

		static Table reduction

		static function autoPawnItemForPlayer takes integer pID, item it returns nothing
			integer val = R2I(GetWidgetLife(it))
			if val>1 then
				DisplayTextToPlayer(Players.players[pID], 0., 0., "Your |cffffcc00"+GetUnitName(Ships.ships[pID])+"|r cannot acquire a |cffffcc00"+GetItemName(it)+"|r; it was automatically pawned for |cffffcc00"+I2S(val)+"|r gold.")
				SetPlayerState(Players.players[pID], PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(Players.players[pID], PLAYER_STATE_RESOURCE_GOLD)+val)
			end
			RemoveItem(it)
		end

		static function getItemClassId takes integer id returns integer
			if classes.hasInt(id) then
				return classes.loadInt(id)
			end
			return CLASS_UNKNOWN
		end

		static function getItemClass takes item it returns integer
			return getItemClassId(GetItemTypeId(it))
		end

		// Checks if a unit has a pair of items with the same item class.
		//
		// Parameters:
		//   unit u:      the unit in question
		//   integer cls: the item class for validation
		//
		// Returns true if `u` has two items of class `cls`
		static function unitHasClassPair takes unit u, integer cls returns boolean
			integer index = 0
			integer count = 0

			loop
				exitwhen index > 5

				if getItemClass(UnitItemInSlot(u, index)) == cls then
					count++
					if count > 1 then
						return true
					end
				end

				index++
			end

			return false
		end

		static function unitHasItemOfClass takes unit u, integer cls returns item
			integer index = 0
			item it = null

			loop
				exitwhen index > 5

				it = UnitItemInSlot(u, index)
				if getItemClass(it) == cls then
					return it
				end

				index++
			end

			return it
		end

		static function unitHasItemOfTypeSlot takes unit u, integer id returns integer
			integer index = 0

			loop
				exitwhen index > 5

				if GetItemTypeId(UnitItemInSlot(u, index)) == id then
					return index
				end

				index++
			end

			return -1
		end

		// Returns true if the unit u has all inventory spaces used.
		static function noItemSlot takes unit u returns boolean
			let count = UnitInventorySize(u)
			int index = 0

			loop
				exitwhen index >= count

				if not GetItemTypeId(UnitItemInSlot(u, index)) > 0 then
					return false
				end

				index++
			end

			return true
		end

		static function unitHasItemOfType takes unit u, integer id returns boolean
			return unitHasItemOfTypeSlot(u, id) > -1
		end

		static function unitHasTypePair takes unit u, integer id returns boolean
			integer index = 0
			integer count = 0
			loop
				exitwhen index>5
				if GetItemTypeId(UnitItemInSlot(u, index)) == id then
					count = count +1
					if count>1 then
						return true
					end
				end
				index= index+1
			end
			return false
		end

		static function unitCanAcquireItemIdPost takes unit u, integer id returns boolean
			integer shipClass = Ships.getClass(u)
			integer itemClass = getItemClassId(id)

			if unitHasClassPair(u, itemClass) and itemClass!= CLASS_UNKNOWN and itemClass != CLASS_CONTRACT and itemClass != CLASS_SPECIAL then
				return false
			elseif unitHasTypePair(u, id) then
				return false
			elseif shipClass == Ships.CLASS_FRIGATE and itemClass== CLASS_CONTRACT then
				return false
			elseif shipClass == Ships.CLASS_SMUGGLER and (itemClass== CLASS_HULL or itemClass== CLASS_SHIELDGEN or id== SHIP_LOCATOR) then
				return false
			elseif shipClass == Ships.CLASS_INTERCEPTOR and itemClass== CLASS_CONTRACT then
				return false
			elseif shipClass == Ships.CLASS_BATTLESHIP and (itemClass== CLASS_TELEPORT or itemClass== CLASS_CONTRACT or id== SHIP_LOCATOR) then
				return false
			end
			return true
		end

		static function unitCanAcquireItemId takes unit u, integer id returns boolean
			integer shipClass = Ships.getClass(u)
			integer itemClass = getItemClassId(id)
			if unitHasItemOfClass(u, itemClass)!= null and itemClass!= CLASS_UNKNOWN and itemClass != CLASS_CONTRACT and itemClass != CLASS_SPECIAL then
				return false
			elseif unitHasItemOfType(u, id) then
				return false
			elseif shipClass == Ships.CLASS_FRIGATE and itemClass== CLASS_CONTRACT then
				return false
			elseif shipClass == Ships.CLASS_SMUGGLER and (itemClass== CLASS_HULL or itemClass== CLASS_SHIELDGEN or id== SHIP_LOCATOR) then
				return false
			elseif shipClass == Ships.CLASS_INTERCEPTOR and itemClass== CLASS_CONTRACT then
				return false
			elseif shipClass == Ships.CLASS_BATTLESHIP and (itemClass== CLASS_TELEPORT or itemClass== CLASS_CONTRACT or id== SHIP_LOCATOR) then
				return false
			end
			return true
		end

		static function playerCanAcquireItem takes integer pID, item it returns boolean
			return unitCanAcquireItemId(Ships.ships[pID], GetItemTypeId(it))
		end

		static function unitCanAcquireItem takes unit u, item it returns boolean
			return unitCanAcquireItemId(u, GetItemTypeId(it))
		end

		static function playerAddItemFromIdConditional takes integer pID, integer id returns boolean
			real x = GetUnitX(Ships.ships[pID])
			real y = GetUnitY(Ships.ships[pID])
			item temp
			if unitCanAcquireItemId(Ships.ships[pID], id) then
				UnitAddItem(Ships.ships[pID], CreateItem(id, x, y))
				return true
			else
				temp = CreateItem(id, 0., 0.)
				autoPawnItemForPlayer(pID, temp)
			end
			return false
		end

		static function playerAddItemsFromIdConditional takes integer pID, integer i0, integer i1, integer i2, integer i3, integer i4, integer i5 returns nothing
			if i0>0 then
				playerAddItemFromIdConditional(pID, i0)
			end
			if i1>0 then
				playerAddItemFromIdConditional(pID, i1)
			end
			if i2>0 then
				playerAddItemFromIdConditional(pID, i2)
			end
			if i3>0 then
				playerAddItemFromIdConditional(pID, i3)
			end
			if i4>0 then
				playerAddItemFromIdConditional(pID, i4)
			end
			if i5>0 then
				playerAddItemFromIdConditional(pID, i5)
			end
		end

		static function playerAddItemsFromId takes integer pID, integer i0, integer i1, integer i2, integer i3, integer i4, integer i5 returns nothing
			real x = GetUnitX(Ships.ships[pID])
			real y = GetUnitY(Ships.ships[pID])
			if i0>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i0, x, y))
			end
			if i1>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i1, x, y))
			end
			if i2>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i2, x, y))
			end
			if i3>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i3, x, y))
			end
			if i4>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i4, x, y))
			end
			if i5>0 then
				UnitAddItem(Ships.ships[pID], CreateItem(i5, x, y))
			end
		end

		static function tradeExperience takes player who returns integer
			return Players.tradeExp[GetPlayerId(who)]
		end

		// Get the cost of a trade contract, in trade experience.
		static function getContractCost takes item it returns integer
			integer typ = GetItemTypeId(it)
			if     typ == DATA_ACQUISIT then
				return 0
			elseif typ == TOXIC_SUBST then
				return 1
			elseif typ == SPIDER_VENOM_CONTRACT then
				return 4
			end
			return -1
		end

		static function pageC takes nothing returns boolean
			item sI= GetSoldItem()
			unit seller = GetTriggerUnit()
			unit buyer = GetBuyingUnit()
			unit toSelect = null
			integer id = GetItemTypeId(sI)
			player owner = GetOwningPlayer(buyer)
			boolean was = false
			if id == PAGE_1_2 then
				was = true
				if seller == Game.westUtility[0] then
					toSelect = Game.westUtility[1]
				else
					toSelect = Game.eastUtility[1]
				end
			elseif id== PAGE_2_1 then
				was = true
				if seller == Game.westUtility[1] then
					toSelect = Game.westUtility[0]
				else
					toSelect = Game.eastUtility[0]
				end
			elseif id== PAGE_2_3 then
				was = true
				if seller == Game.westUtility[1] then
					toSelect = Game.westUtility[2]
				else
					toSelect = Game.eastUtility[2]
				end
			elseif id== PAGE_3_2 then
				was = true
				if seller == Game.westUtility[2] then
					toSelect = Game.westUtility[1]
				else
					toSelect= Game.eastUtility[1]
				end
			elseif id== PAGE_3_4 then
				was = true
				if seller == Game.westUtility[2] then
					toSelect = Game.westUtility[3]
				else
					toSelect = Game.eastUtility[3]
				end
			elseif id== PAGE_4_3 then
				was = true
				if seller == Game.westUtility[3] then
					toSelect = Game.westUtility[2]
				else
					toSelect = Game.eastUtility[2]
				end
			elseif id== KPAGE_1_2 then
				was = true
				if seller == Game.westKeep[0] then
					toSelect = Game.westKeep[1]
				else
					toSelect = Game.eastKeep[1]
				end
			elseif id== KPAGE_2_1 then
				was = true
				if seller == Game.westKeep[1] then
					toSelect = Game.westKeep[0]
				else
					toSelect = Game.eastKeep[0]
				end
			end
			if was then
				RemoveItem(sI)
				if Players.localPlayer == owner then
					SelectUnit(seller, false)
					SelectUnit(toSelect, true)
				end
			end

			return false
		end

		static function resetUnitDamageReduction takes unit u returns nothing
			integer index = 0
			loop
				exitwhen index>5
				if GetItemTypeId(UnitItemInSlot(u, index)) == STONE_HULL then
					reduction.saveInt(u.getHandleId(), REDUCTION1)
					return
				end
				index = index +1
			end

			reduction.saveInt(u.getHandleId(), 0)
		end

		static function c takes nothing returns boolean
			unit buyer = GetBuyingUnit()
			item sI= GetSoldItem()
			player buyerP = GetOwningPlayer(buyer)
			real val
			integer class = getItemClass(sI)
			boolean has = unitHasItemOfType(buyer, GetItemTypeId(sI))
			item it = unitHasItemOfClass(buyer, class)

			if has or GetItemTypeId(sI) == GetItemTypeId(it) then
				simError(buyerP, "You can't carry two of the same equipment.")
				val = GetWidgetLife(sI)
				if val>1. then
					SetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD) + R2I(val))
				end
				RemoveItem(sI)
			elseif not unitCanAcquireItem(buyer, sI) then
				val = GetWidgetLife(sI)
				RemoveItem(sI)
				SetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD) + R2I(val))
				simError(buyerP, WRONG_CLASS)
			else
				if it!= null and class!= CLASS_UNKNOWN and class!= CLASS_SPECIAL and class!= CLASS_CONTRACT then
					val = GetWidgetLife(it)
					SetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(buyerP, PLAYER_STATE_RESOURCE_GOLD) + R2I(val))
					DisplayTextToPlayer(buyerP, 0., 0., "|cffffcc00Note:|r Your item of type |cffffcc00"+GetItemName(it)+"|r was automatically sold for |cffffcc00"+I2S(R2I(val))+"|r gold to accomodate your new |cffffcc00"+GetItemName(sI)+"|r.")
					RemoveItem(it)
				elseif class== CLASS_CONTRACT and getContractCost(sI)>tradeExperience(buyerP) then
					RemoveItem(sI)
					simError(buyerP, INSUFFICIENT_EXPERIENCE)
				end
			end

			resetUnitDamageReduction(buyer)
			return false
		end
	end

	init
		registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELL_ITEM, function Equipment.c)
		registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELL_ITEM, function Equipment.pageC)

		// Initialize class table
		Equipment.classes = new Table
		Equipment.classes.saveInt(Equipment.STEAM_CORE,            Equipment.CLASS_ENGINE)
		Equipment.classes.saveInt(Equipment.PROBLEM_SOLVER,        Equipment.CLASS_MECHANIC)
		Equipment.classes.saveInt(Equipment.STONE_HULL,            Equipment.CLASS_HULL)
		Equipment.classes.saveInt(Equipment.BRONZE_GEN,            Equipment.CLASS_SHIELDGEN)
		Equipment.classes.saveInt(Equipment.IONIC_TELEPORT,        Equipment.CLASS_TELEPORT)
		Equipment.classes.saveInt(Equipment.SHIP_LOCATOR,          Equipment.CLASS_SPECIAL)
		Equipment.classes.saveInt(Equipment.DATA_ACQUISIT,         Equipment.CLASS_CONTRACT)
		Equipment.classes.saveInt(Equipment.TOXIC_SUBST,           Equipment.CLASS_CONTRACT)
		Equipment.classes.saveInt(Equipment.SPIDER_VENOM_CONTRACT, Equipment.CLASS_CONTRACT)

		Equipment.reduction = new Table
	end
