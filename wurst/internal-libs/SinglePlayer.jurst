package SinglePlayer
    import Players

    public class SinglePlayer
        static constant string SP_PREFIX = "[|cffffcc00Single Player|r] "
        static constant string TAB       = "    "

        static          player thePlayer
        static          bool   fastEnabled = false

        static method help takes nothing returns bool
            BJDebugMsg(SP_PREFIX + "Available commands:")
            BJDebugMsg(TAB + "\\h, \\help: Display this help menu.")
            BJDebugMsg(TAB + "\\c, \\clear: Clear the on-screen text messages.")
            BJDebugMsg(TAB + "\\e, \\experience, \\tradeExperience: Gives 500 trading experience.")
            BJDebugMsg(TAB + "\\v, \\visibility: Disables Fog of War and Black Mask.")
            BJDebugMsg(TAB + "\\f, \\fast: Enables instant ship movement.")

            return false
        end

        static method experience takes nothing returns bool
            BJDebugMsg(SP_PREFIX + "500 trading experience added.")
            Players.tradeExp[thePlayer.getId()] += 500

            return false
        end

        static method visibility takes nothing returns bool
            BJDebugMsg(SP_PREFIX + "Full visibility enabled.")
            FogMaskEnable(false)
            FogEnable(false)

            return false
        end

        static method fast takes nothing returns bool
            BJDebugMsg(SP_PREFIX + "Fast movement enabled.")
            fastEnabled = true

            return false
        end

        static method clear takes nothing returns bool
            // Debug message redacted to avoid waste.
            ClearTextMessages()

            return false
        end

        static method enable takes nothing returns nothing
            BJDebugMsg(SP_PREFIX + "Single player mode enabled.  Enter \\help for a list of commands.")

            int i = 2
            loop
                exitwhen i > 11

                if Players.playing[i] then
                    thePlayer = Players.players[i]
                    exitwhen true
                end

                i++
            end

            trigger help = CreateTrigger()
            help..registerPlayerChatEvent(thePlayer, "\\h",    true)
                ..registerPlayerChatEvent(thePlayer, "\\help", true)
                ..addCondition(Condition(function help))

            trigger experience = CreateTrigger()
            experience..registerPlayerChatEvent(thePlayer, "\\e",               true)
                      ..registerPlayerChatEvent(thePlayer, "\\experience",      true)
                      ..registerPlayerChatEvent(thePlayer, "\\tradeExperience", true)
                      ..addCondition(Condition(function experience))

            trigger visibility = CreateTrigger()
            visibility..registerPlayerChatEvent(thePlayer, "\\v",          true)
                      ..registerPlayerChatEvent(thePlayer, "\\visibility", true)
                      ..addCondition(Condition(function visibility))

            trigger clear = CreateTrigger()
            clear..registerPlayerChatEvent(thePlayer, "\\c",     true)
                 ..registerPlayerChatEvent(thePlayer, "\\clear", true)
                 ..addCondition(Condition(function clear))

            trigger fast = CreateTrigger()
            fast..registerPlayerChatEvent(thePlayer, "\\f",      true)
                ..registerPlayerChatEvent(thePlayer, "\\fast",   true)
                ..addCondition(Condition(function fast))
        end
    end